# CloudFront distribution to serve the static website (root domain)
resource "aws_cloudfront_distribution" "website" {
  enabled     = true
  price_class = "PriceClass_100"
  aliases     = [var.root_domain_name, "www.${var.root_domain_name}"]

  origin {
    origin_id   = "origin-bucket-${var.web_bucket_id}"
    domain_name = var.website_endpoint

    custom_origin_config {
      origin_protocol_policy = "http-only" # S3 only supports http
      http_port              = 80
      https_port             = 443
      origin_ssl_protocols   = ["TLSv1.2", "TLSv1.1", "TLSv1"]
    }
  }

  default_root_object = "index.html"

  default_cache_behavior {
    allowed_methods  = ["GET", "HEAD", "OPTIONS"]
    cached_methods   = ["GET", "HEAD", "OPTIONS"]
    target_origin_id = "origin-bucket-${var.web_bucket_id}"
    min_ttl          = "0"
    default_ttl      = "300"
    max_ttl          = "1200"

    viewer_protocol_policy = "redirect-to-https" # Redirects any HTTP request to HTTPS
    compress               = true

    forwarded_values {
      query_string = false

      headers = [
        "Origin",
        "Access-Control-Request-Headers",
        "Access-Control-Request-Method",
      ]

      cookies {
        forward = "none"
      }
    }

  }

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }

  viewer_certificate {
    acm_certificate_arn = "${aws_acm_certificate.website_domain.arn}"
    ssl_support_method  = "sni-only"
  }

  custom_error_response {
    error_caching_min_ttl = 300
    error_code            = 404
    response_page_path    = "/404.html"
    response_code         = 404
  }

  depends_on = [aws_acm_certificate.website_domain]
}